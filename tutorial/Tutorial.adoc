= Tutorial

== cmake Basics

This section is not meant to be a full tutorial on cmake, but hopefully is enough to get you started.

=== Introduction to cmake

CMake is a two step build process.  The developer writes a CMakeLists.txt file in the cmake language.  Using your CMakeLists.txt file, cmake creates a makefile (or similar) defining how to create a set of output files from a set of input files.  It also keeps track of what outputs need to be regenerated if changes are made to the inputs.

The makefile (or similar) is then used to generate the output files from the input files.

When using any build system like cmake, there are several key components:

* input files
* recipes
* recipe dependencies
* output files
* targets

A recipe is a set of instructions to convert a set of input files into a set of output files.

Input files to a recipe can either be input files, or outputs generated from other recipes.

A recipe dependency is a file that, if changed, implies that the recipe should be rerun.  For example, if you edit a verilog source file, you will need to rebuild a bit file that was generated from that verilog file.  The bit file depends on the verilog file.

This is different than a source dependency.  Only output files can have recipe dependencies because the recipe dependencies are what are used to generate a file.  A source file's source dependency is used by that source file, but is not used to produce that source file.

In cmake, any specified recipe dependency must be a file that already exists or that cmake has a recipe that describes that file as an output.

A target (sometimes called a phony target) is a named fake output that always needs to be rebuilt.  Using the command "make <target name>" produces all of the outputs that were specified as dependencies of the target (and all of the dependencies necessary to create that target).  The recipe dependencies of the target "ALL" are produced if the command "make" is used with no output or target specified.

=== Getting started

In the top level directory of your project, create a text file called CMakeLists.txt

The first two lines of your project should define the minimum version of cmake that can be used for your project and the project name.  All the versions of vivado I checked provide cmake version 3.3.

You can add comments by starting a line with the # symbol.

[source,cmake]
make_minimum_required(VERSION 3.3)
project(helloworld)

I recommend that you force the user to build out of tree (create a separate build folder to put all outputs in).  You can accomplish this by adding the lines

[source,cmake]
set(CMAKE_DISABLE_SOURCE_CHANGES ON)
set(CMAKE_DISABLE_IN_SOURCE_BUILD ON)

Variables don't need to be declared before being assigned.  You can assign variables using the "set" function and access them by prepending the variable name by a dollar sign and surrounding it with braces (e.g. "${varname}")

[source,cmake]
set(mymessage "Hello World!")

You can output messages to the user when cmake is run by using the "message" function

[source,cmake]
message(STATUS ${mymessage})

Our CMakeLists.txt file now looks like this

[source,cmake]
cmake_minimum_required(VERSION 3.3)
project(helloworld)
set(CMAKE_DISABLE_SOURCE_CHANGES ON)
set(CMAKE_DISABLE_IN_SOURCE_BUILD ON)
set(mymessage "Hello World!")
message(STATUS ${mymessage})

To run cmake, create a build directory in your project adjacent to the CMakeLists.txt file, navigate to that build directory, and run "cmake .." from command line.  If all goes well, it will welcome you to the world of cmake.

=== File paths and includes

In cmake, filepaths should use forward slashes, not backslashes, even if you are on windows.

To be able to use the functions in viv-prj-gen, you need to get viv-prj-gen on your computer, and to use the "include" function on the path to the .cmake file with the functions you want in it.

I recommend using viv-prj-gen as a git submodule or a git subtree.

The start of your CMakeLists.txt might look like this
[source,cmake]
cmake_minimum_required(VERSION 3.3)
project(helloworld)
set(CMAKE_DISABLE_SOURCE_CHANGES ON)
set(CMAKE_DISABLE_IN_SOURCE_BUILD ON)
include(viv-prj-gen/vivgen_functions.cmake)
include(viv-prj-gen/cmake_utils/file_functions.cmake)

Included cmake files have their own scope for variables.  The parent variables are copied into the include cmake file's environment.  The included cmake file can explicitly set variables in the parent scope.  This is often used in functions to return by value.

=== Getting the Source Files

There are a number of ways of specifying filenames in cmake.  There are several goals when specifying filepaths in cmake.

Paths that you write should be relative (so that you can move from machine to machine or change the directory you store stuff in).

The paths that you use should be absolute (so that the user doesn't have to know from what directory you are referring to).

The following sections has several methods.  The appendix lists another one.

==== GLOB

The easiset way to get filepaths in cmake to use is file(GLOB varname path_to_file).  file glob sets the passed variable to a list of full paths of exisiting files that match the passed path.

Some cmake users recommend against using glob in cmake because it will silently fail to detect files added after running cmake.

If you had the following source tree

[source, verbatim]
.
├── build
├── CMakeLists.txt
├── hdl
│   ├── DFlipFlop.vhdl
│   └── GenericDFlipFlop.vhdl
└── tb
    └── DFlipFlop_tb.vhdl

in your CMakeLists.txt file might have something like

[source, cmake]
file(GLOB hdlfiles hdl/*)
file(GLOB tbfiles tb/*)

we might want to add message function calls to check to make sure that cmake is finding the files as expected.  We end up with the cmake file

[source,cmake]
cmake_minimum_required(VERSION 3.3)
project(globfilelists)
set(CMAKE_DISABLE_SOURCE_CHANGES ON)
set(CMAKE_DISABLE_IN_SOURCE_BUILD ON)
file(GLOB hdlfiles hdl/*.vhdl)
file(GLOB tbfiles tb/*.vhdl)
message(STATUS "hdlfiles ${hdlfiles}")
message(STATUS "tbfiles ${tbfiles}")

WARNING: glob only works on files that exist when cmake is run.  Use a different approach for absolute paths to generated files.

This prints a semicolon separated lists of absolute paths to the files in hdl and tb.

==== get_filename_component

An absolute path can be gotten from a relative path using the function get_filename_component with the REALPATH mode like so

[source, cmake]
get_filename_component(hdlfile hdl/DFlipFlop.vhdl REALPATH)
message(STATUS "hdlfile ${hdlfile}")

We can use a foreach loop over a list to call this function on every filename in the list.

[source, cmake]
set(filenames
  hdl/DFlipFlop.vhdl
  hdl/GenericDFlipFlop.vhdl
  )
foreach(filename ${filenames})
  get_filename_component(hdlfile ${filename} REALPATH)
  message(STATUS "file ${hdlfile}")
endforeach()


==== read_filelist

More experienced build system users tend to recommend having an explicit list of files, rather than globing for the files in a directory.

This can be accomplished by explicitly listing the files in the cmakelists file, like we did in the get_filename_component example.

However, putting the list of source in a separate file than your CMakeLists.txt file makes generating this list easier, enables you to reuse the file list across different build systems, and reduces clutter.

The cmake_utils repo provides a function a function called "read_filelist" to specify filepaths.  To use this function, create a file that has a relative file paths of the files you want in your list.

for example, in my hdl folder, I might have a listfile.txt file that says

[source, verbatim]
DFlipFlop.v
ShiftReg.v

Then, in your CMakeLists.txt file, call the read_filelist function.  The same example as before might look something like this.

[source,cmake]
----
cmake_minimum_required(VERSION 3.3)
project(filelists)
set(CMAKE_DISABLE_SOURCE_CHANGES ON)
set(CMAKE_DISABLE_IN_SOURCE_BUILD ON)

include(viv-prj-gen/cmake_utils/file_functions.cmake)

get_filename_component(hdllistfile hdl/filelist.txt REALPATH)
get_filename_componnet(tblistfile tb/filelist.txt REALPATH)

read_filelist(hdlfiles ${hdllistfile})

message(STATUS "hdlfiles ${hdlfiles}")
----

on a tree that that looks like this

[source, verbatim]
.
├── build
├── CMakeLists.txt
├── hdl
│   ├── DFlipFlop.v
│   ├── filelist.txt
│   └── ShiftReg.v
└── viv-prj-gen
    ├── cmake_utils/file_functions.cmake
    └── vivgen_functions.cmake

==== add_dependency_tree_file


If you want to build lots of outputs based on your input set, sometimes it is helpful to represent your sources by what other sources they depend on.

cmake_utils provides a set of 2 functions that are used together to accomplish this.

add_dependency_tree_file reads a file and saves the source dependencies that are listed in it.

get_list_from_dependency_tree reads those source dependencies and generates a list from it.

If we had a file tree that looked like this

[source, verbatim]
.
├── build
├── CMakeLists.txt
├── hdl
│   ├── blinky.v
│   ├── blinky_zedboard_z7.v
│   └── deptree.txt
├── simhdl
│   └── vlog_tb_utils.v
├── tb
|   ├── blinky_tb.v
|   └── deptree.txt
└── viv-prj-gen
    ├── cmake_utils/file_functions.cmake
    └── vivgen_functions.cmake

Let's say

* blinky_zedboard_z7.v  instantiates blinky.v 
* blinky_tb.v instantiates blinky.v vlog_tb_utils.v

tb/deptree.txt could look like this:

[source, verbatim]
blinky_tb.v <= ../hdl/blinky.v ../simhdl/vlog_tb_utils.v

and hdl/deptree.txt could look like this

[source, verbatim]
blinky_zedboard_z7.v <= blinky.v

Using add_dependency_tree_file and get_list_from_dependency_tree to get a list of source files, based on the top filename, would look like this: 

[source, cmake]
----
get_filename_component(hdldepfile hdl/deptree.txt REALPATH)
get_filename_component(tbdepfile tb/deptree.txt REALPATH)

add_dependency_tree_file(placeholder1 ${hdldepfile})
add_dependency_tree_file(placeholder2 ${tbdepfile})

get_filename_component(topsynth hdl/blinky_zedboard_z7.v REALPATH)
get_filename_component(topsim tb/blinky_tb.v REALPATH)
get_list_from_dependency_tree(synthfiles ${topsynth})
get_list_from_dependency_tree(simfiles ${topsim})

message(STATUS "synth files ${synthfiles}")
message(STATUS "sim files ${simfiles}")
----

== Vivado Project Generation

A Vivado project is useful for interacting with the code using Vivado's gui.  This is great for nonautomated simulations.

Let's generate one.  We are going to use the add_vivado_devel_project function.  Documentation for functions in vivgen_functions.cmake is in vivgen_functions.adoc .

The first thing we need to do is include our .cmake files and get paths to our hdl files.  Lets say our file system, looks like this.

[source, verbatim]
.
├── build
├── CMakeLists.txt
├── constraints
│   ├── blinky_zedboard.xdc
│   └── filelist.txt
├── hdl
│   ├── blinky.v
│   ├── blinky_zedboard_z7.v
│   └── deptree.txt
├── simhdl
│   └── vlog_tb_utils.v
├── tb
|   ├── blinky_tb.v
|   └── deptree.txt
└── viv-prj-gen
    ├── cmake_utils/file_functions.cmake
    └── vivgen_functions.cmake


We need to include our two cmake function files and find our source code.

[source, cmake]
----
include(viv-prj-gen/vivgen_functions.cmake)
include(viv-prj-gen/utils_functions.cmake)

get_filename_component(hdldepfile hdl/deptree.txt REALPATH)
get_filename_component(tbdepfile tb/deptree.txt REALPATH)

#full path to constraint file
get_filename_component(xdclistfile constraints/filelist.txt REALPATH)

add_dependency_tree_file(placeholder1 ${hdldepfile})
add_dependency_tree_file(placeholder2 ${tbdepfile})

#get list of constraint files
read_filelist(xdcfiles ${xdclistfile})

#the value of topsynth is also in placeholder1
get_filename_component(topsynth hdl/blinky_zedboard_z7.v REALPATH)
#the value of topsim is also in placeholder2
get_filename_component(topsim tb/blinky_tb.v REALPATH)
get_list_from_dependency_tree(synthfiles ${topsynth})
get_list_from_dependency_tree(simfiles ${topsim})
----

The add_vivado_devel_project requires us to give it a name for its project, a partname for the chip the project is for, and some kind of source files.  It uses named arguments.  The constraint file design was written for a zynq chip.  Let's call our project "blinky".

[source, cmake]
----
set(partname "XC7Z020-1CLG484")

add_vivado_devel_project(
  PRJNAME blinky
  PARTNAME ${partname}
  VERILOGSYNTHFILES ${synthfiles}
  VERILOGSIMFILES ${simfiles}
  UNSCOPEDLATEXDC ${xdcfiles}
  )
----

That's all we need.  Our file will now look like this

[source, cmake]
----
cmake_minimum_required(VERSION 3.3)
project(project)

#cmake_utils file_functions.cmake provides the functions
#add_dependency_tree_file and get_list_from_dependency_tree
include(viv-prj-gen/cmake_utils/file_functions.cmake)

#vivgen_functions.cmake provides the add_vivado_devel_project function
include(viv-prj-gen/vivgen_functions.cmake)

#we need the full paths to the dependency tree files
get_filename_component(hdldepfile hdl/deptree.txt REALPATH)
get_filename_component(tbdepfile tb/deptree.txt REALPATH)

#full path to constraint file
get_filename_component(xdclistfile constraints/filelist.txt REALPATH)


#placeholder1 and placeholder2 store the filepaths that are the heads of lists
add_dependency_tree_file(placeholder1 ${hdldepfile})
add_dependency_tree_file(placeholder2 ${tbdepfile})

#get list of constraint files
read_filelist(xdcfiles ${xdclistfile})

#the value of topsynth is also in placeholder1
get_filename_component(topsynth hdl/blinky_zedboard_z7.v REALPATH)
#the value of topsim is also in placeholder2
get_filename_component(topsim tb/blinky_tb.v REALPATH)
get_list_from_dependency_tree(synthfiles ${topsynth})
get_list_from_dependency_tree(simfiles ${topsim})
  
set(partname "XC7Z020-1CLG484")

add_vivado_devel_project(
  PRJNAME blinky
  VERILOGSYNTHFILES ${synthfiles}
  VERILOGSIMFILES ${simfiles}
  PARTNAME ${partname}
  UNSCOPEDLATEXDC ${xdcfiles}
  )
----

If we navigate into the build directory, run "cmake ..", then "make help", it will print something like

[source, verbatim]
The following are some of the valid targets for this Makefile:
... all (the default if no target is provided)
... clean
... depend
... rebuild_cache
... edit_cache
... blinky_genvivprj

The last option is the target that our function added.  source your vivado distributions settings64.sh or settings64.bat to tell your cmdline where your vivado is.  Then run "make test_genvivprj" from your build directory.  Vivado will run and generate the project!

The generated xilinx xpr project file will be generated in the subdirectory of the build directory described in the tree below.  Open that xpr file with vivado and start developing!  Whenever you add or remove source files, edit your deptree.txt and filelist.txt files accordingly.

[source, verbatim]
.
└── XC7Z020-1CLG484
    └── devel_prjs
        └── blinky
            ├── blinky.cache
            │   └── wt
            │       └── project.wpc
            ├── blinky.hw
            │   └── blinky.lpr
            ├── blinky.ip_user_files
            └── blinky.xpr


== nonproject flow bitfile generation

Generating a project to develop in is helpful, but we need to be able to generate bit files from source.  We don't want to use the development project that isn't source controlled to do this, as doing so would not create a reproducible build.

To generate a bit file, we use the add_vivado_nonprj_bitfile function in vivgen_functions.cmake (documented in vivgen_functions.adoc).

To illustrate this, we will add on to the blinky example used to explain the add_vivado_devel_project function.

As in the previous example, our file tree looks like this.

[source, verbatim]
.
├── build
├── CMakeLists.txt
├── constraints
│   ├── blinky_zedboard.xdc
│   └── filelist.txt
├── hdl
│   ├── blinky.v
│   ├── blinky_zedboard_z7.v
│   └── deptree.txt
├── simhdl
│   └── vlog_tb_utils.v
├── tb
|   ├── blinky_tb.v
|   └── deptree.txt
└── viv-prj-gen
    ├── cmake_utils/file_functions.cmake
    └── vivgen_functions.cmake


Just like last time, we need to get the lists of source files to pass to our cmake function

[source, cmake]
----
include(viv-prj-gen/vivgen_functions.cmake)
include(viv-prj-gen/utils_functions.cmake)

get_filename_component(hdldepfile hdl/deptree.txt REALPATH)

#full path to constraint file
get_filename_component(xdclistfile constraints/filelist.txt REALPATH)

add_dependency_tree_file(placeholder1 ${hdldepfile})

#get list of constraint files
read_filelist(xdcfiles ${xdclistfile})

#the value of topsynth is also in placeholder1
get_filename_component(topsynth hdl/blinky_zedboard_z7.v REALPATH)
get_list_from_dependency_tree(synthfiles ${topsynth})
----

The function call to define a recipe for a bit file is fairly similar.  The bit file doesn't need the testbench.  It needs to know what the name of the top module is, and we need to get from it the location of the output bitfile.

[source, cmake]
add_vivado_nonprj_bitfile(
  PRJNAME blinky
  TOPNAME blinky_zedboard_z7
  PARTNAME ${partname}
  VERILOGFILES ${synthfiles}
  UNSCOPEDLATEXDC ${xdcfiles}
  BITFILE_OUTPUT blinky_bitfile
  )

This creates a recipe for a bitfile, and stores the location it would put the bitfile in the variable blinky_bitfile.

To get this recipe to run, we need to set up a target that depends on that file.  cmake has a function for this called "add_custom_target".

[source, cmake]
add_custom_target(blinky_nonprj
  DEPENDS ${blinky_bitfile}
  )

Now, if we navigate to our build directory, and run

[source, bash]
cmake..; make help;

make help will list blinky_nonprj as an option, and running make blinky_nonprj will generate our bit file.  The entire cmake file is listed below.

[source,cmake]
----
cmake_minimum_required(VERSION 3.3)
project(project)

#cmake_utils file_functions.cmake provides the functions
#add_dependency_tree_file and get_list_from_dependency_tree
include(viv-prj-gen/cmake_utils/file_functions.cmake)

#vivgen_functions.cmake provides the add_vivado_devel_project function
include(viv-prj-gen/vivgen_functions.cmake)

#we need the full paths to the dependency tree files
get_filename_component(hdldepfile hdl/deptree.txt REALPATH)
get_filename_component(tbdepfile tb/deptree.txt REALPATH)

#full path to constraint file
get_filename_component(xdclistfile constraints/filelist.txt REALPATH)


#placeholder1 and placeholder2 store the filepaths that are the heads of lists
add_dependency_tree_file(placeholder1 ${hdldepfile})
add_dependency_tree_file(placeholder2 ${tbdepfile})

#get list of constraint files
read_filelist(xdcfiles ${xdclistfile})

#the value of topsynth is also in placeholder1
get_filename_component(topsynth hdl/blinky_zedboard_z7.v REALPATH)
#the value of topsim is also in placeholder2
get_filename_component(topsim tb/blinky_tb.v REALPATH)
get_list_from_dependency_tree(synthfiles ${topsynth})
get_list_from_dependency_tree(simfiles ${topsim})
  
set(partname "XC7Z020-1CLG484")

add_vivado_devel_project(
  PRJNAME blinky
  VERILOGSYNTHFILES ${synthfiles}
  VERILOGSIMFILES ${simfiles}
  PARTNAME ${partname}
  UNSCOPEDLATEXDC ${xdcfiles}
  )

add_vivado_nonprj_bitfile(
  PRJNAME blinky
  TOPNAME blinky_zedboard_z7
  PARTNAME ${partname}
  VERILOGFILES ${synthfiles}
  UNSCOPEDLATEXDC ${xdcfiles}
  BITFILE_OUTPUT blinky_bitfile
  )

add_custom_target(blinky_nonprj
  DEPENDS ${blinky_bitfile}
  )
----

== hardware description file generation
== Generating a XACT IP wrapper
== Generating a xci file
